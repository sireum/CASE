package MC
public
	
	with SW;
	with TB_SYS;
	with Base_Types;
	
	-- The radio communication hardware
	device Radio
		features
			recv_map_in: in event data port SW::MISSING_TYPE.Impl;
			recv_map_out: out event data port SW::Command.Impl;
			send_status_in: in event data port SW::Coordinate.Impl;
			send_status_out: out event data port SW::MISSING_TYPE.Impl;
	end Radio;
	
	device implementation Radio.Impl	
	end Radio.Impl;


	-- The serial communication hardware
	device UART
		features
			waypoint_in: in event data port SW::MissionWindow.Impl;
			waypoint_out: out event data port SW::MISSING_TYPE.Impl;
			position_status_in: in event data port SW::MISSING_TYPE.Impl;
			position_status_out: out event data port SW::Coordinate.Impl;
	end UART;
	
	device implementation UART.Impl
	end UART.Impl;
	
	
	-- The Wifi communication hardware
	device Wifi
		features
			gimbal_command_in: in event data port SW::MISSING_TYPE.Impl;
			gimbal_command_out: out event data port SW::MISSING_TYPE.Impl;
	end Wifi;
	
	device implementation Wifi.Impl
	end Wifi.Impl;


	-- The Mission Computer processor
	processor MC_Proc			
	end MC_Proc;
	
	processor implementation MC_Proc.Impl
		properties
			TB_SYS::OS => CAmkES;
			TB_SYS::HW => TK1;
			TB_SYS::Add_Dummy_Arg_To_Void_Fns => True;
	end MC_Proc.Impl;
	
	
	-- The Mission Computer RAM
	memory MC_Mem	
	end MC_Mem;
	
	memory implementation MC_Mem.Impl
	end MC_Mem.Impl;


	-- The MissionComputer onboard the UAV
	system MissionComputer
		
		features
			recv_map: in event data port SW::MISSING_TYPE.Impl;
			position_status: in event data port SW::MISSING_TYPE.Impl;
			waypoint: out event data port SW::MISSING_TYPE.Impl;
			send_status: out event data port SW::MISSING_TYPE.Impl;
			gimbal_command: out event data port SW::MISSING_TYPE.Impl;

	end MissionComputer;
	
	system implementation MissionComputer.Impl
		
		subcomponents
			RADIO_HW: device Radio.Impl;
			UART_HW: device UART.Impl;
			PROC_HW: processor MC_Proc.Impl;
			MEM_HW: memory MC_Mem.Impl;
			PROC_SW: process SW::MC_SW.Impl;
			WIFI_HW: device Wifi.Impl;
			
		connections
			c1: port recv_map -> RADIO_HW.recv_map_in;
			c2: port RADIO_HW.recv_map_out -> PROC_SW.recv_map;
			c3: port PROC_SW.send_status -> RADIO_HW.send_status_in;
			c4: port RADIO_HW.send_status_out -> send_status;
			c5: port PROC_SW.waypoint -> UART_HW.waypoint_in;
			c6: port UART_HW.waypoint_out -> waypoint;
			c7: port position_status -> UART_HW.position_status_in;
			c8: port UART_HW.position_status_out -> PROC_SW.position_status;
			c9: port PROC_SW.gimbal_command -> WIFI_HW.gimbal_command_in;
			c10: port WIFI_HW.gimbal_command_out -> gimbal_command;
		
		properties
			Actual_Processor_Binding => (reference (PROC_HW)) applies to PROC_SW;
			Actual_Memory_Binding => (reference (MEM_HW)) applies to PROC_SW;
		
	end MissionComputer.Impl;
	
end MC;